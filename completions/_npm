#compdef npm

# customized from https://github.com/lukechilds/zsh-better-npm-completion

_zbnc_npm_command() {
  echo "${words[2]}"
}

_zbnc_npm_command_arg() {
  echo "${words[3]}"
}

_zbnc_no_of_npm_args() {
  echo "$#words"
}

_zbnc_list_cached_modules() {
  ls ~/.npm 2>/dev/null
}

_zbnc_recursively_look_for() {
  local filename="$1"
  local dir=$PWD
  while [ ! -e "$dir/$filename" ]; do
    dir=${dir%/*}
    [[ "$dir" = "" ]] && break
  done
  [[ ! "$dir" = "" ]] && echo "$dir/$filename"
}

_zbnc_get_package_json_property_object() {
  local package_json="$1"
  local property="$2"
  cat "$package_json" |
    sed -nE "/^  \"$property\": \{$/,/^  \},?$/p" | # Grab scripts object
    sed '1d;$d' |                                   # Remove first/last lines
    sed -E 's/    "([^"]+)": "(.+)",?/\1=>\2/'      # Parse into key=>value
}

_zbnc_get_package_json_property_object_keys() {
  local package_json="$1"
  local property="$2"
  _zbnc_get_package_json_property_object "$package_json" "$property" | cut -f 1 -d "="
}

_zbnc_parse_package_json_for_script_suggestions() {
  local package_json="$1"
  _zbnc_get_package_json_property_object "$package_json" scripts |
    sed -E 's/(.+)=>(.+)/\1:$ \2/' |  # Parse commands into suggestions
    sed 's/\(:\)[^$]/\\&/g' |         # Escape ":" in commands
    sed 's/\(:\)$[^ ]/\\&/g'          # Escape ":$" without a space in commands
}

_zbnc_parse_package_json_for_deps() {
  local package_json="$1"
  _zbnc_get_package_json_property_object "$package_json" dependencies
  _zbnc_get_package_json_property_object "$package_json" devDependencies
}

_zbnc_parse_package_json_for_deps_suggestions() {
  local package_json="$1"
  _zbnc_parse_package_json_for_deps "$package_json" |
    sed -E 's/(.+)=>(.+)/\1/' |  # Parse commands into suggestions
    sed 's/\(:\)[^$]/\\&/g' |         # Escape ":" in commands
    sed 's/\(:\)$[^ ]/\\&/g'          # Escape ":$" without a space in commands
}

_zbnc_npm_install_completion() {
  # Only run on `npm install ?`
  [[ ! "$(_zbnc_no_of_npm_args)" -ge "3" ]] && return

  [[ ${#words[-1]} -eq 0 ]] && return

  local npm_search=$(npm search "${words[-1]}" --parseable --prefer-offline | cut -f 1,2,5 --output-delimiter=':')

  local -a package_names
  local -a package_descriptions
  echo "$npm_search" | while IFS=: read -r package_name package_description package_version; do
    package_names+=("$package_name")
    if [[ ${#package_version} -le 10 ]]; then
      package_descriptions+=("$package_name:\[$package_version\]\ - $package_description")
    else
      package_descriptions+=("$package_name:$package_description")
    fi
  done

  _describe 'npm packages' package_descriptions package_names

  # Make sure we don't run default completion
  custom_completion=true
}

_zbnc_npm_uninstall_completion() {

  # Use default npm completion to recommend global modules
  [[ "$(_zbnc_npm_command_arg)" = "-g" ]] ||  [[ "$(_zbnc_npm_command_arg)" = "--global" ]] && return

  # Look for a package.json file
  local package_json="$(_zbnc_recursively_look_for package.json)"

  # Return if we can't find package.json
  [[ "$package_json" = "" ]] && return

  local -a options
  options=(${(f)"$(_zbnc_parse_package_json_for_deps_suggestions $package_json)"})

  # Return if we can't parse it
  [[ "$#options" = 0 ]] && return

  # Load the completions
  _describe 'currently installed dependencies' options

  # Make sure we don't run default completion
  custom_completion=true
}

_zbnc_npm_run_completion() {

  # Only run on `npm run ?`
  [[ ! "$(_zbnc_no_of_npm_args)" = "3" ]] && return

  # Look for a package.json file
  local package_json="$(_zbnc_recursively_look_for package.json)"

  # Return if we can't find package.json
  [[ "$package_json" = "" ]] && return

  # Parse scripts in package.json
  local -a options
  options=(${(f)"$(_zbnc_parse_package_json_for_script_suggestions $package_json)"})

  # Return if we can't parse it
  [[ "$#options" = 0 ]] && return

  # Load the completions
  _describe 'scripts' options

  # Make sure we don't run default completion
  custom_completion=true
}

_zbnc_default_npm_completion() {
  compadd -- $(COMP_CWORD=$((CURRENT-1)) \
              COMP_LINE=$BUFFER \
              COMP_POINT=0 \
              npm completion -- "${words[@]}" \
              2>/dev/null)
}

_zbnc_zsh_better_npm_completion() {

  # Store custom completion status
  local custom_completion=false

  # Load custom completion commands
  case "$(_zbnc_npm_command)" in
    i|install|add|in|ins|inst|insta|instal|isnt|isnta|isntal|isntall)
      _zbnc_npm_install_completion
      ;;
    r|uninstall|rm|remove|unlink|un)
      _zbnc_npm_uninstall_completion
      ;;
    run|run-script|rum|urn)
      _zbnc_npm_run_completion
      ;;
  esac

  # Fall back to default completion if we haven't done a custom one
  [[ $custom_completion = false ]] && _zbnc_default_npm_completion
}

if [ "$funcstack[1]" = "_zbnc_zsh_better_npm_completion" ]; then
    _zbnc_zsh_better_npm_completion "$@"
elif (( $+commands[npm] )); then
    compdef _zbnc_zsh_better_npm_completion npm
fi
